plugins {
    id "maven-publish"
    id "org.jreleaser"
}

group "com.navercorp.eventeria"
version "1.3.2-SNAPSHOT"

tasks.withType(Javadoc).configureEach { enabled = false }

java {
    withJavadocJar()
    withSourcesJar()
}

jreleaser {
    release {
        github {
            skipRelease = true
        }
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    sign = true
                    stagingRepository('build/staging-deploy')
                }
            }
            // The following configuration is applied when the version matches the "*-SNAPSHOT" pattern.
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = false
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            repositories {
                maven {
                    url = layout.buildDirectory.dir("staging-deploy")
                }
            }

            pom {
                name = "eventeria"
                description = "Event Driven light weight message format."
                url = "http://github.com/naver/eventeria"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "mhyeon-lee"
                        name = "Myeonghyeon Lee"
                        email = "mheyon.lee@gmail.com"
                    }
                    developer {
                        id = "ilkyun-lim"
                        name = "Ilkyun Lim"
                        email = "ilkyun.lim@navercorp.com"
                    }
                    developer {
                        id = "sanha"
                        name = "Sanha Lee"
                        email = "sanhaleehana@gmail.com"
                    }
                    developer {
                        id = "taes-k"
                        name = "Taeseong Kim"
                        email = "cobura12@gmail.com"
                    }
                    developer {
                        id = "chanhyeong"
                        name = "Chanhyeong Cho"
                        email = "chu7825@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/naver/eventeria.git"
                    developerConnection = "scm:git:ssh://github.com/naver/eventeria.git"
                    url = "http://github.com/naver/eventeria"
                }
            }
        }
    }
}

jar {
    manifest {
        attributes(
                "Specification-Title": artifactName,
                "Specification-Version": project.version,
                "Specification-Vendor": "com.navercorp",
                "Implementation-Title": artifactName,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "com.navercorp"
        )
    }
}
